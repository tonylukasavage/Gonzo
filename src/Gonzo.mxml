<?xml version="1.0" encoding="utf-8"?>
<s:WindowedApplication xmlns:fx="http://ns.adobe.com/mxml/2009" 
			   xmlns:s="library://ns.adobe.com/flex/spark" 
			   xmlns:mx="library://ns.adobe.com/flex/mx" 
			   xmlns:gonzo="com.savagelook.Gonzo.*"
			   minWidth="200" minHeight="200" width="800" height="600" 
			   initialize="init();"
			   creationComplete="initUI();"
			   skinClass="com.savagelook.Gonzo.skins.GonzoAppSkin">
	<fx:Script>
		<![CDATA[
			import com.adobe.linguistics.spelling.SpellUI;
			import com.savagelook.Gonzo.GonzoSeparatorEvent;
			
			import mx.core.mx_internal;

			private static const preHtml:String = "<html><head><style>h1 { color:#ff0000; }</style></head><body>";
			private static const postHtml:String = "</body></html>";
			private static const APP_NAME:String = "Gonzo";
			private static const EDITOR_WIDTH_MINIMUM:Number = 100;
			private static const DEFAULT_FILENAME:String = "Untitled.md"
			
			public var currentMarkdownFile:String = "";
			private var currentCssFile:String = "";
			
			protected function init():void
			{
				// determine OS and setup menu
				gonzoMenu.isWin = (Capabilities.os.toLowerCase().indexOf("windows") >= 0);
				gonzoMenu.isMac = (Capabilities.os.toLowerCase().indexOf("mac os") >= 0);	
				gonzoMenu.gonzo = this;
				
				// resize the markdown and html editors when the separator is moved
				gonzoSeparator.addEventListener(GonzoSeparatorEvent.SEPARATOR_MOVED, function(event:GonzoSeparatorEvent):void {
					if (event.stageX - mainGroupLayout.paddingLeft > EDITOR_WIDTH_MINIMUM &&
						event.stageX < mainGroup.width - mainGroupLayout.paddingRight - EDITOR_WIDTH_MINIMUM) {
						markdownEditor.width = event.stageX - 12;
					}
				});
				
				// update Gonzo whenever we type in the markdown editor
				markdownEditor.addEventListener(KeyboardEvent.KEY_UP, function(event:KeyboardEvent):void {
					clearInterval(updateGonzoInterval);
					updateGonzoInterval = setInterval(updateGonzo, 1000);
				});
			}

			private var updateGonzoInterval:uint;
			private var vsp:Number = 0;
			private var isUpdatingHtml:Boolean = false;
			
			protected function initUI():void {
				// TODO check for recently loaded files, otherwise
				this.updateGonzo(DEFAULT_FILENAME);
				SpellUI.enableSpelling(markdownEditor, "en_US");
				
				htmlPreview.addEventListener(Event.COMPLETE, function(e:Event):void {
					(e.target as HTML).verticalScrollPosition = vsp;
				});
			}
			
			private function updateTitle(filename:String):void {
				if (filename != "") {
					this.title = APP_NAME + " - " + filename;
				}
			}

			public function updateGonzo(filename:String = ""):void {		
				var html:String = Showdown.makeHtml(markdownEditor.text);
				var rawText:String = StringUtils.stripTags(html);
				var wordCount:uint = StringUtils.wordCount(rawText);
				vsp = htmlPreview.verticalScrollPosition;
				
				htmlPreview.htmlText = preHtml + html + postHtml;
				this.statusText.text = "Word Count: " + wordCount.toString();
				this.updateTitle(filename);	
			}
		]]>
	</fx:Script>

	<s:menu>
		<gonzo:GonzoNativeMenu id="gonzoMenu"/>
	</s:menu>	
	
	<s:Group width="100%" height="100%" id="mainGroup">
		<s:layout>
			<s:HorizontalLayout gap="0" paddingLeft="10" paddingRight="10" paddingTop="10" paddingBottom="10" id="mainGroupLayout"/>
		</s:layout>
		
		<gonzo:GonzoTextArea id="markdownEditor" height="100%" width="100%"
							 fontFamily="Monaco" fontSize="16"/>
		<gonzo:GonzoSeparator id="gonzoSeparator"/>
		<mx:HTML id="htmlPreview" width="100%" height="100%" tabEnabled="false" tabFocusEnabled="false"
				 borderColor="#000000" borderStyle="solid" borderVisible="true"/>
	</s:Group>
</s:WindowedApplication>
