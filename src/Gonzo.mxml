<?xml version="1.0" encoding="utf-8"?>
<s:WindowedApplication xmlns:fx="http://ns.adobe.com/mxml/2009" 
			   xmlns:s="library://ns.adobe.com/flex/spark" 
			   xmlns:mx="library://ns.adobe.com/flex/mx" 
			   xmlns:gonzo="com.savagelook.Gonzo.*"
			   minWidth="200" minHeight="200" width="800" height="600" 
			   initialize="init();"
			   creationComplete="initUI();"
			   skinClass="com.savagelook.Gonzo.skins.GonzoAppSkin">
	<fx:Script>
		<![CDATA[
			import com.adobe.linguistics.spelling.SpellUI;
			import com.savagelook.Gonzo.GonzoSeparatorEvent;
			import com.savagelook.Gonzo.GonzoUtils;
			
			import mx.controls.Alert;
			import mx.controls.ProgressBar;
			import mx.events.CloseEvent;
			
			[SkinPart(required="true", type="mx.controls.ProgressBar")]
			public var progressBar:ProgressBar;

			private static const preHtml:String = "<html><head><style>h1 { color:#ff0000; }</style></head><body>";
			private static const postHtml:String = "</body></html>";
			private static const APP_NAME:String = "Gonzo";
			private static const EDITOR_WIDTH_MINIMUM:Number = 150;
			public static const DEFAULT_FILENAME:String = "Untitled.md"
			
			public var currentMarkdownFile:String = "";
			private var updateGonzoInterval:uint;
			private var htmlVerticalScrollPosition:Number = 0;
			public var isEditorModified:Boolean = false;
			
			protected function init():void
			{
				// determine OS and setup menu
				gonzoMenu.isWin = (Capabilities.os.toLowerCase().indexOf("windows") >= 0);
				gonzoMenu.isMac = (Capabilities.os.toLowerCase().indexOf("mac os") >= 0);	
				gonzoMenu.gonzo = this;	
				GonzoUtils.DEFAULT_FILENAME = Gonzo.DEFAULT_FILENAME;
			}
			
			protected function initUI():void {
				// TODO check for recently loaded files, otherwise
				SpellUI.enableSpelling(markdownEditor, "en_US");
				
				// resize the markdown and html editors when the separator is moved
				gonzoSeparator.addEventListener(GonzoSeparatorEvent.SEPARATOR_MOVED, function(event:GonzoSeparatorEvent):void {
					if (event.stageX - mainGroupLayout.paddingLeft > EDITOR_WIDTH_MINIMUM &&
						event.stageX < mainGroup.width - mainGroupLayout.paddingRight - EDITOR_WIDTH_MINIMUM) {
						editorTabNavigator.width = event.stageX - 12;
					}
				});
				
				// update Gonzo whenever we type in the markdown or css editor
				markdownEditor.addEventListener(Event.CHANGE, this.doEditorUpdate);
				cssEditor.addEventListener(Event.CHANGE, this.doEditorUpdate);
				
				// maintain the vertical scroll position of the html preview
				htmlRendered.addEventListener(Event.COMPLETE, function(e:Event):void {
					(e.target as HTML).verticalScrollPosition = htmlVerticalScrollPosition;
					progressBar.visible = false;
				});
				
				this.updateGonzo(DEFAULT_FILENAME);
			}
			
			private var markdownBuffer:String = "";
			public function doEditorUpdate(e:Event):void {
				this.isEditorModified = true;
				clearInterval(updateGonzoInterval);
				progressBar.visible = true;
				updateGonzoInterval = setInterval(updateGonzo, 500);
			}

			public function updateGonzo(filename:String = ""):void {	
				clearInterval(updateGonzoInterval);
				
				var markdown:String = this.markdownEditor.text;
				var wordCount:uint = StringUtils.wordCount(markdown);
				var rawHtml:String = Showdown.makeHtml(markdown);
				var fullHtml:String = this.constructFullHtml(rawHtml);
				
				if (filename == "") {
					filename = this.currentMarkdownFile == "" ? DEFAULT_FILENAME : this.currentMarkdownFile;
				}
				
				this.markdownEditor.pushUndo(markdown);
				this.cssEditor.pushUndo(this.cssEditor.text);
				
				this.htmlVerticalScrollPosition = this.htmlRendered.verticalScrollPosition;	
				this.htmlRendered.htmlText = fullHtml;
				this.htmlRaw.text = rawHtml;
				this.statusText.text = "Word Count: " + wordCount.toString();
				this.title = APP_NAME + " - " + filename + (this.isEditorModified ? "*" : "");
			}
			
			private function constructFullHtml(rawHtml:String):String {
				const html1:String = "<html><head>";
				const html2:String = "</head><body>";
				const html3:String = "</body></html>";
				
				var html:String = html1;
				if (cssEditor.text != "") {
					html += "<style>" + cssEditor.text + "</style>";
				}
				html += html2 + rawHtml + html3;
				
				return html;
			}
			
			public function checkForUnsavedEditors(command:Function):void {
				if (!this.isEditorModified) {
					command();
					return;
				}

				Alert.show(
					"'" + (this.currentMarkdownFile == "" ? DEFAULT_FILENAME : this.currentMarkdownFile) + "' has been modified.  Would you like to save it?",
					"Save your changes?",
					Alert.YES | Alert.NO | Alert.CANCEL,
					this,
					function(event:CloseEvent):void {
						if (event.detail == Alert.YES) {
							gonzoMenu.doFileSave();
							command();
						} else if (event.detail == Alert.NO) {
							command();
						} else if (event.detail == Alert.CANCEL) {
							return;
						}	
					}
				);
			}
		]]>
	</fx:Script>

	<s:menu>
		<gonzo:GonzoNativeMenu id="gonzoMenu"/>
	</s:menu>	
	
	<s:Group width="100%" height="100%" id="mainGroup">
		<s:layout>
			<s:HorizontalLayout gap="0" paddingLeft="10" paddingRight="10" paddingTop="10" paddingBottom="10" id="mainGroupLayout" verticalAlign="bottom"/>
		</s:layout>
		
		<mx:TabNavigator id="editorTabNavigator" width="100%" height="100%" creationPolicy="all" borderStyle="solid" paddingTop="0" paddingBottom="0">
			<mx:VBox label="markdown">
				<gonzo:GonzoTextArea id="markdownEditor" height="100%" width="100%" fontFamily="Monaco" fontSize="16"/>	
			</mx:VBox>
			<mx:VBox label="css" id="cssEditorTab">
				<gonzo:GonzoTextArea id="cssEditor" height="100%" width="100%" fontFamily="Monaco" fontSize="16"/>
			</mx:VBox>		
		</mx:TabNavigator>
		
		<gonzo:GonzoSeparator id="gonzoSeparator" height="{markdownEditor.height}"/>
		
		<mx:TabNavigator id="cssTabNavigator" width="100%" height="100%" creationPolicy="all" borderStyle="solid" paddingTop="0" paddingBottom="0">
			<mx:VBox label="preview">
				<mx:HTML id="htmlRendered" height="100%" width="100%" borderStyle="solid"/>	
			</mx:VBox>
			<mx:VBox label="html">
				<s:TextArea id="htmlRaw" height="100%" width="100%" editable="false" fontFamily="Monaco" fontSize="16"/>
			</mx:VBox>		
		</mx:TabNavigator>
	</s:Group>
</s:WindowedApplication>
