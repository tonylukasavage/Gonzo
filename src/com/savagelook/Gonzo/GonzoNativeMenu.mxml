<?xml version="1.0" encoding="utf-8"?>
<mx:FlexNativeMenu xmlns:fx="http://ns.adobe.com/mxml/2009" 
				   xmlns:s="library://ns.adobe.com/flex/spark" 
				   xmlns:mx="library://ns.adobe.com/flex/mx"
				   showRoot="false" 
				   labelField="@label" 
				   keyEquivalentField="@keyEquivalent" 
				   keyEquivalentModifiersFunction="keyEquivalentModifiers"
				   dataProvider="{gonzoMenuData}"
				   itemClick="gonzoItemClickHandler(event)">
	<fx:Declarations>
		<fx:XML format="e4x" id="gonzoMenuData">
			<root>
				<menuitem label="File">
					<menuitem label="New...">
						<menuitem label="Markdown &amp; CSS" keyEquivalent="n" controlKey="true" id="file_new"/>
						<menuitem label="Markdown" id="file_newMarkdown"/>
						<menuitem label="CSS" id="file_newCss"/>
					</menuitem>
					<menuitem label="Open..." keyEquivalent="o" controlKey="true" id="file_open"/>
					<menuitem label="Save" keyEquivalent="s" controlKey="true" id="file_save"/>
					<menuitem label="Save As..." keyEquivalent="s" controlKey="true" shiftKey="true" id="file_saveAs"/>
					<menuitem type="separator"/>
					<menuitem label="Export HTML..." keyEquivalent="h" controlKey="true" id="file_export_html"/>
					<menuitem type="separator"/>
					<menuitem label="Quit" keyEquivalent="q" controlKey="true" id="file_exit"/>
				</menuitem>
				<menuitem label="Edit">
					<menuitem label="Undo" keyEquivalent="z" controlKey="true" id="edit_undo"/>
					<menuitem type="separator"/>
					<menuitem label="Cut" keyEquivalent="x" controlKey="true" id="edit_cut"/>
					<menuitem label="Copy" keyEquivalent="c" controlKey="true" id="edit_copy"/>
					<menuitem label="Paste" keyEquivalent="v" controlKey="true" id="edit_paste"/>
					<menuitem type="separator"/>
					<menuitem label="Select All" keyEquivalent="a" controlKey="true" id="edit_selectAll"/>
				</menuitem>
				<menuitem label="Format">
					<menuitem label="Select Font..." id="format_font" keyEquivalent="f" controlKey="true" shiftKey="true"/>
					<menuitem label="Word Wrap" id="format_wordWrap" type="check" toggled="true" keyEquivalent="w" controlKey="true" shiftKey="true"/>
				</menuitem>
				<menuitem label="Window">
					<menuitem label="Show CSS" id="window_css" type="check" toggled="true" keyEquivalent="c" controlKey="true" shiftKey="true"/>
					<menuitem label="Show Preview" id="window_preview" type="check" toggled="true" keyEquivalent="p" controlKey="true"/>
				</menuitem>
				<menuitem label="Help">
					<menuitem label="Tutorial" id="help_tutorial" keyEquivalent="t" controlKey="true" shiftKey="true"/>
				</menuitem>
			</root>
		</fx:XML>	
	</fx:Declarations>
	<fx:Script>
		<![CDATA[
			import mx.events.FlexNativeMenuEvent;
			import mx.printing.FlexPrintJob;
			public var isWin:Boolean = false;
			public var isMac:Boolean = false;
			public var gonzo:Gonzo = null;
			
			private function doFileNew():void {
				gonzo.markdownEditor.text = "";
				gonzo.cssEditor.text = "";
				gonzo.currentMarkdownFile = Gonzo.DEFAULT_FILENAME;
				gonzo.isEditorModified = false;
				gonzo.updateGonzo();
			}
			
			private function doFileOpen():void {
				var file:File = new File();
				file.addEventListener(Event.SELECT, function(event:Event):void {
					var openedFile:File = event.target as File;
					gonzo.markdownEditor.text = GonzoUtils.readStringFromFile(openedFile);
					gonzo.currentMarkdownFile = openedFile.nativePath;
					gonzo.progressBar.visible = true;
					gonzo.isEditorModified = false;
					gonzo.updateGonzo(gonzo.currentMarkdownFile);
				});
				file.browseForOpen("Open a markdown file...");
			}
			
			private function doFileSaveAs():void {
				var file:File = new File();
				file.addEventListener(Event.COMPLETE, function(event:Event):void {
					var savedFile:File = event.target as File;
					gonzo.currentMarkdownFile = savedFile.nativePath;
					gonzo.isEditorModified = false;
					gonzo.updateGonzo(gonzo.currentMarkdownFile);
				});
				
				var filename:String = gonzo.currentMarkdownFile == "" ? "Untitled.md" : GonzoUtils.getFilenameFromPath(gonzo.currentMarkdownFile);
				file.save(GonzoUtils.stripNonPrintableCharacters(gonzo.markdownEditor.text), filename);
			}
			
			public function doFileSave():void {
				if (gonzo.currentMarkdownFile == "") {
					this.doFileSaveAs();
					return;
				} 
				
				GonzoUtils.writeStringToFilename(gonzo.currentMarkdownFile, gonzo.markdownEditor.text);	
				gonzo.isEditorModified = false;
				gonzo.updateGonzo(gonzo.currentMarkdownFile);
			}
			
			private function doFileExportHtml():void {
				GonzoUtils.writeStringToFilename(gonzo.currentMarkdownFile + ".html", Showdown.makeHtml(gonzo.markdownEditor.text));
			}
			
			private function doEditUndo():void {
				var label:String = gonzo.editorTabNavigator.selectedChild.label;
				var editor:GonzoTextArea;
				if (label == "markdown") {
					editor = gonzo.markdownEditor;
				} else if (label == "css") {
					editor = gonzo.cssEditor;
				}
				
				var undo:String = editor.popUndo();
				if (undo != null) {
					editor.text = undo;
					gonzo.doEditorUpdate(new Event(""));
				}
			}

			protected function gonzoItemClickHandler(event:FlexNativeMenuEvent):void
			{
				var node:XML = event.item as XML;
				var id:String = node.@id;
				var file:File;
			
				if (id == "file_new") {
					gonzo.checkForUnsavedEditors(doFileNew);
				} else if (id == "file_open") {
					gonzo.checkForUnsavedEditors(doFileOpen);
				} else if (id == "file_save") {
					this.doFileSave();
				} else if (id == "file_saveAs") {
					this.doFileSaveAs();
				}  else if (id == "file_export_html") {
					this.doFileExportHtml();
				} else if (id == "file_exit") {
					gonzo.checkForUnsavedEditors(NativeApplication.nativeApplication.exit);
				} else if (id == "edit_undo") { 
					this.doEditUndo();
				} else if (id == "edit_redo") { 
					// TODO
				}else if (id == "edit_cut") { 
					NativeApplication.nativeApplication.cut();
				} else if (id == "edit_copy") { 
					NativeApplication.nativeApplication.copy();
				} else if (id == "edit_paste") { 
					NativeApplication.nativeApplication.paste();
				} else if (id == "edit_selectAll") { 
					NativeApplication.nativeApplication.selectAll();
				} else if (id == "window_css") {
					if (node.@toggled == "true") {
						gonzo.editorTabNavigator.addChild(gonzo.cssEditorTab);
					} else {
						gonzo.editorTabNavigator.removeChild(gonzo.cssEditorTab);
						gonzo.markdownEditor.setFocus();
					}
				} else if (id == "window_preview") {
					if (node.@toggled == "true") {
						gonzo.gonzoSeparator.visible = gonzo.gonzoSeparator.includeInLayout = true;
						gonzo.previewTabNavigator.visible = gonzo.previewTabNavigator.includeInLayout = true;
					} else {
						gonzo.gonzoSeparator.visible = gonzo.gonzoSeparator.includeInLayout = false;
						gonzo.previewTabNavigator.visible = gonzo.previewTabNavigator.includeInLayout = false;
						gonzo.editorTabNavigator.percentWidth = 100;
					}
				} else if (id == "format_wordWrap") {
					gonzo.markdownEditor.wordWrap = node.@toggled == "true";
					gonzo.cssEditor.wordWrap = node.@toggled == "true";
				} else {
					trace(event.label + "(" + id + ")");
				}
			}
			
			/**
			 * Handles key strokes in a platform specific way.  CTRL == CTRL on Windows,
			 * CTRL == COMMAND on Mac.
			 * 
			 * @param item An object that holds the menu entry for the keyEquivalent modifier
			 * 
			 * @return An array containing the modified keys pressed
			 */
			private function keyEquivalentModifiers(item:Object):Array
			{
				var result:Array = new Array();
				
				var keyEquivField:String = this.keyEquivalentField;
				var altKeyField:String;
				var ctrlKeyField:String;
				var shiftKeyField:String;
				if (item is XML)
				{
					altKeyField = "@altKey";
					ctrlKeyField = "@controlKey";
					shiftKeyField = "@shiftKey";
				}
				else if (item is Object)
				{
					altKeyField = "altKey";
					ctrlKeyField = "controlKey";
					shiftKeyField = "shiftKey";
				}
				
				if (item[keyEquivField] == null || item[keyEquivField].length == 0)
				{
					return result;
				}
				
				if (item[altKeyField] != null && item[altKeyField] == true)
				{
					if (isWin)
					{
						result.push(Keyboard.ALTERNATE);
					}
				}
				
				if (item[ctrlKeyField] != null && item[ctrlKeyField] == true)
				{
					if (isWin)
					{
						result.push(Keyboard.CONTROL);
					}
					else if (isMac)
					{
						result.push(Keyboard.COMMAND);
					}
				}
				
				if (item[shiftKeyField] != null && item[shiftKeyField] == true)
				{
					result.push(Keyboard.SHIFT);
				}
				
				return result;
			}

		]]>
	</fx:Script>
</mx:FlexNativeMenu>
