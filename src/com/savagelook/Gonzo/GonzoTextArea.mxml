<?xml version="1.0" encoding="utf-8"?>
<mx:TextArea xmlns:fx="http://ns.adobe.com/mxml/2009" 
			xmlns:s="library://ns.adobe.com/flex/spark" 
			xmlns:mx="library://ns.adobe.com/flex/mx"
			keyFocusChange="onKeyFocusChange(event)"
			creationComplete="onCreationComplete(event)"
			>
	<fx:Script>
		<![CDATA[
			import com.adobe.linguistics.spelling.SpellUI;
			
			import mx.core.mx_internal;
			import mx.events.FlexEvent;
			use namespace mx_internal;
			
			private static const UNDO_BUFFER_SIZE:uint = 20;
			
			private var _undoStack:Vector.<String> = new Vector.<String>();
			private var _undoNext:String = null;
			private var _fullPath:String;
			private var _isModified:Boolean = false;
			private var _updateInterval:uint;
			
			protected function onCreationComplete(event:FlexEvent):void
			{
				SpellUI.enableSpelling(this, "en_US");
			}
			
			protected function onKeyFocusChange(event:FocusEvent):void
			{
				event.preventDefault();
				this.textField.replaceSelectedText("\t");
			}
			
			public function getCaretIndex():int {
				return this.getTextField().caretIndex;
			}
			
			public function updateEditor():void {
				clearInterval(this.updateInterval);
				this.pushUndo(this.text);
				this.dispatchEvent(new Event(Event.COMPLETE));	
			}
			
			public function pushUndo(string:String):void {
				while (_undoStack.length >= UNDO_BUFFER_SIZE) {
					_undoStack.pop();
				} 
				
				if (_undoNext != string) {
					if (_undoNext != null) {
						_undoStack.unshift(_undoNext);
					}
					_undoNext = string;
				}
			}
			
			public function popUndo():String {
				var undo:String;
				if (_undoStack.length > 0) {
					undo = _undoStack.shift();
					_undoNext = undo;
				} else {
					undo = null;
				}
				
				return undo;
			}
			
			////////// Helper Functions //////////
			public function printUndoStack():void {
				for (var i:uint = 0; i < _undoStack.length; i++) {
					trace("STACK[" + i.toString() + "]:" + _undoStack[i]);
				}
				trace("NEXT:" + _undoNext);
				trace("");
			}
			
			////////// Getters and Setters //////////
			public function get filename():String
			{
				return GonzoUtils.getFilenameFromPath(_fullPath);
			}
			
			public function get fullPath():String
			{
				return _fullPath;
			}
			
			public function set fullPath(value:String):void
			{
				_fullPath = value;
			}
			
			public function get isModified():Boolean
			{
				return _isModified;
			}
			
			public function set isModified(value:Boolean):void
			{
				_isModified = value;
			}
			
			public function get updateInterval():uint
			{
				return _updateInterval;
			}
			
			public function set updateInterval(value:uint):void
			{
				_updateInterval = value;
			}
		]]>
	</fx:Script>
</mx:TextArea>
